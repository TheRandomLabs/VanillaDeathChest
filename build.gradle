import com.modrinth.minotaur.TaskModrinthUpload

plugins {
	id "java-library"
	id "fabric-loom" version "0.8-SNAPSHOT"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "1.2.1"
	id "org.cadixdev.licenser" version "0.6.1"
}

ext {
	commonGradleBranch = "master"
	minecraftVersion = "1.17"
	yarnMappings = "1.17+build.13"
	loaderVersion = "0.11.3"
	fabricAPIVersion = "0.36.0+1.17"
	clothConfigVersion = "5.0.34"
	modMenuVersion = "2.0.2"
	autoConfigVersion = "3.3.1"
}
version = "2.1.0-fabric"
group = "com.therandomlabs.vanilladeathchest"
archivesBaseName = "vanilladeathchest"

apply from: "https://raw.githubusercontent.com/jhbuchanan45/Common-Gradle/main/fabric.gradle"

if (project.hasProperty("curseForgeAPIKey")) {
	curseforge {
		apiKey = project.curseForgeAPIKey

		project {
			id = "393000"

			addGameVersion "Fabric"
			addGameVersion "Java 16"
			addGameVersion "1.17.0"

			mainArtifact(remapJar) {
				changelogType = "markdown"
				changelog = file("changelog.md")
				displayName = "VanillaDeathChest ${version}"
				releaseType = "alpha"

				relations {
					requiredDependency "fabric-api"
				}
			}

			addArtifact(sourcesJar) {
				displayName = "VanillaDeathChest ${version} Sources"
				releaseType = "release"
			}

			addArtifact(javadocJar) {
				displayName = "VanillaDeathChest ${version} Javadoc"
				releaseType = "release"
			}
		}

		options {
			forgeGradleIntegration = false
		}
	}

	afterEvaluate {
		tasks.curseforge393000.dependsOn build
	}
}

if (project.hasProperty("modrinthToken")) {
	task publishModrinth(type: TaskModrinthUpload) {
		token = project.modrinthToken
		projectId = "WtzGQgcZ"
		versionNumber = version
		versionName = "VanillaDeathChest ${version}"
		changelog = new File("changelog.md").getText()
		uploadFile = remapJar
		releaseType = "alpha"
		addGameVersion("1.17.0")
		addLoader("fabric")
		addFile(sourcesJar)
		addFile(javadocJar)
	}
}
