buildscript {
	repositories {
		jcenter()

		maven {
			url "https://www.dimdev.org/maven/"
		}

		maven {
			url "http://repo.spongepowered.org/maven"
		}
	}

	dependencies {
		classpath "org.dimdev:ForgeGradle:2.3-SNAPSHOT"
		classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
	}
}

apply plugin: "net.minecraftforge.gradle.tweaker-client"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"

version = "1.13-1.8.0.1-rift"
group = "com.therandomlabs.vanilladeathchest"
archivesBaseName = "vanilladeathchest"

sourceCompatibility = targetCompatibility = "1.8"

repositories {
	mavenCentral()

	maven {
		url "https://www.dimdev.org/maven/"
	}
}

dependencies {
	implementation "org.dimdev:rift:1.0.4-66:dev"
	shadow "blue.endless:jankson:1.1.0"
	compile "blue.endless:jankson:1.1.0"
}

shadowJar {
	classifier = ""
	relocate "blue.endless.jankson", "com.therandomlabs.vanilladeathchest.repackage.blue.endless.jankson"
	configurations = [project.configurations.shadow]
}

reobf {
	shadowJar {
		mappingType = "NOTCH"
	}
}

build.dependsOn shadowJar

artifacts {
	archives shadowJar
}

mixin {
	defaultObfuscationEnv notch
	add sourceSets.main, "mixins.vanilladeathchest.refmap.json"
}

minecraft {
	version = "1.13"
	runDir = "run"
	mappings = "snapshot_20180823"
	tweakClass = "org.dimdev.riftloader.launch.RiftLoaderClientTweaker"

	replace "@FINGERPRINT@", project.findProperty("signSHA1")
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty("keyStore")
	}

	keyStore = project.findProperty("keyStore")
	alias = project.findProperty("keyStoreAlias")
	storePass = project.findProperty("keyStorePass")
	keyPass = project.findProperty("keyStoreKeyPass")
	inputFile = jar.archivePath
	outputFile = jar.archivePath
}

build.dependsOn signJar

